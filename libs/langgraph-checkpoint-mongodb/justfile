set shell := ["bash", "-c"]
set dotenv-load
set dotenv-filename := "../../.local_atlas_uri"

# Default target executed when no arguments are given.
[private]
default:
  @just --list

install:
	poetry install --with dev

[group('test')]
integration_tests:
	poetry run pytest tests/integration_tests/

[group('test')]
test *args="tests/unit_tests":
	poetry run pytest {{args}}

[group('test')]
test_watch filename:
	poetry run ptw --snapshot-update --now . -- -vv {{filename}}

[group('lint')]
lint:
	git ls-files -- '*.py' | xargs poetry run pre-commit run ruff --files
	git ls-files -- '*.py' | xargs poetry run pre-commit run ruff-format --files

[group('lint')]
typing:
	poetry run mypy .

[group('lint')]
codespell:
	git ls-files -- '*.py' | xargs poetry run pre-commit run --hook-stage manual codespell --files
